plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

// (temporary) Dependabot workaround ~ awaits https://github.com/dependabot/dependabot-core/issues/1618
project.ext.springBootVersion = '4.0.2'

group = 'multiple.plugin.test.demo'
version = demoPluginVersion

dependencies {
    compileOnly "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"

    testImplementation 'org.spockframework:spock-core:2.4-groovy-4.0'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

gradlePlugin {
    plugins {
        build {
            id = 'multiple-plugin-test-demo'
            displayName = 'Multiple plugin test demonstration'
            implementationClass = 'multiple.plugin.test.demo.DemoPlugin'
        }
    }
}

publishing {
    repositories {
        maven {
            name = 'testMavenLocal'
            url = layout.buildDirectory.dir('tmp/test/mvn-repo-override')
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

tasks.test.dependsOn(tasks.publishAllPublicationsToTestMavenLocalRepository)

test {
    useJUnitPlatform()

    systemProperty 'springBootVersionWorkaround' , project.ext.springBootVersion

    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}
